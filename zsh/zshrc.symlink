# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/src

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

OHMYZSH="~/.oh-my-zsh"

# Set name of the theme to load.
# Look in ~/.oh-my-zsh/themes/
# Optionally, if you set this to "random", it'll load a random theme each
# time that oh-my-zsh is loaded.
ZSH_THEME="geometry/geometry"

GEOMETRY_PROMPT_PLUGINS=(exec_time git hg virtualenv jobs)
GEOMETRY_PROMPT_PREFIX=""
PROMPT_GEOMETRY_GIT_TIME=false
PROMPT_GEOMETRY_GIT_CONFLICTS=true

# Red dots are displayed while waiting for completion
COMPLETION_WAITING_DOTS="true"

# Which plugins would you like to load? (plugins can be found in ~/.oh-my-zsh/plugins/*)
# Example format: plugins=(rails git textmate ruby lighthouse)
plugins=(ansible archlinux git gitignore docker virtualenv python pass mosh colored-man-pages)

source $OHMYZSH/oh-my-zsh.sh

[ -x /usr/bin/lesspipe ] && eval "$(lesspipe)"

# Rehash the completion database on every command
zstyle ':completion:*' rehash true

# Escape shell symbols in scp
zstyle -e :urlglobber url-other-schema '[[ $words[1] == scp || $words[1] == rsync ]] && reply=("*") || reply=(http https ftp)'

# My shell supports 256 colors
[ $TERM = "xterm" ] && export TERM="xterm-256color"
[ $TERM = "screen-bce" ] && export TERM="screen-256color"

# Ensure gpgagent uses the right TTY
echo UPDATESTARTUPTTY | gpg-connect-agent 2>&1 >/dev/null

# added by travis gem
[ -f ~/.travis/travis.sh ] && source ~/.travis/travis.sh
